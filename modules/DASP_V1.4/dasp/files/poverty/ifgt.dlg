/*************************************************************************/
/* DASP  : Distributive Analysis Stata Package  (version 1.4)          */
/*************************************************************************/
/* Conceived and programmed by Dr. Araar Abdelkrim  (2006)               */
/* Université Laval, Quebec, Canada                                    */
/* email : aabd@ecn.ulaval.ca                                            */
/* Phone : 1 418 656 7507                                                */
/*************************************************************************/ 

VERSION 9.2


INCLUDE graph_header
//INCLUDE header
POSITION . . 760 300


DIALOG main, label("DASP| FGT and EDE-FGT Index --> ifgt command") tabtitle("Main")


BEGIN
  GROUPBOX intvar 10     10    360  60,                      ///
           label("Variable(s) of interest: ")
           
  VARLIST  vl_var 20     +25   340   .,                   ///
           label("Variables")
           
           
           
  TEXT     tx_var2      20    100    80      .,                 ///
           label("Size  variable:   ")  
  VARNAME  vn_hhs      110    @     250      .,                 ///
           option(hsize)      
  TEXT     tx_var4      20    +25    80      .,                 ///
           label("Group  variable:   ") 
  VARNAME  vn_hhg      110    @     250      .,                 ///
           option(hgroup)            


  GROUPBOX percentile 390    10    360 80,                      ///
           label("Index option(s)")
           
  
  TEXT     txt_op1     400    +25    80      .,                 ///  
             label("Index:   ")     
  
  COMBOBOX  cb_index      500    @     240      .,                 ///
              dropdownlist                                          ///
              contents(cb_index)  values(cb_index_val) option(index)  ///
            default(fgt)
 
            
  TEXT       txt_op2     400    +25    80      .,                 ///  
             label("Type:   ")                             ///      
          
  COMBOBOX  cb_nor      500    @     240      .,                  ///
            dropdownlist                                          ///
            contents(cb_nor)  values(cb_nor_val) option(type)     ///
            default(nor)
            
           
  GROUPBOX chk_per  390    90  360 60,                         ///
           label("Parameter(s): ")         ///
  
    TEXT txt_al     +10    +25    100      .,                 ///  
                  label("Parameter alpha:   ")                           
       
    EDIT  ed_al    +100    @     240      .,                 ///
              option(alpha)    default(0)    
           
  
   
  GROUPBOX chk_pl  390    150  360 130,                         ///
           label("Poverty line: ")         ///
           
           
  RADIO    rb_a      400     +25    80      .,			///
		 onclickon(program main_rb)  onclickoff(program main_rb) first		///
		 label("Absolute:")
  
  EDIT    ed_pl   500    @     240      .,                 ///
           default(10000) option(pline)	  	
           
  RADIO    rb_r     400         +30        80         .,			///
  		 onclickon(program main_rb) last		///
  		 label("Relative:")        
   
  SPINNER prop    500  @   40 ., ///
          min(1) max(100) default(50) label("% of the ")  option(prop) 
          
  TEXT    ofthe    540  @   40 ., ///
           label("% of the ")      		
   
   
   COMBOBOX  cb_pl      580    @     70      .,                  ///
             dropdownlist                                          ///
             contents(cb_pl)  values(cb_pl_val) option(opl)     ///
             default(me)   onselchangelist(cond_change)
             
             
   TEXT    ofpr    650  @   60 ., ///
           label("at percentile")  
           
   EDIT perc      710  @   30 ., ///
           default(0.40) label("pr")   option(perc)   
           
   TEXT      txtrel    500 +20   240 ., ///
           label("If group variable is used, poverty line is relative to:") 
           
   COMBOBOX  cb_rela      500    +20     200      .,                  ///
             dropdownlist                                          ///
             contents(cb_rel)  values(cb_rel_val) option(rel)     ///
             default(popul)   
   DEFINE _x 20
   DEFINE _y 240
     
  INCLUDE _bu_svyset
END



PROGRAM main_rb
BEGIN
if main.rb_a  {
call main.ed_pl.enable

call main.prop.disable
call main.ofthe.disable
call main.cb_pl.disable
call main.ofpr.disable
call main.perc.disable
call main.txtrel.disable
call main.cb_rela.disable
}

if !main.rb_a  {
call main.ed_pl.disable

call main.prop.enable
call main.ofthe.enable
call main.cb_pl.enable
call main.ofpr.enable
call main.perc.enable
call main.txtrel.enable
call main.cb_rela.enable
}

END



SCRIPT PREINIT
BEGIN
	create BOOLEAN has_cond1
	create BOOLEAN has_cond2
	create BOOLEAN has_cond3	
END

LIST cond_change
BEGIN
	script cond1
	script cond2
	script cond3
	
END


SCRIPT cond1
BEGIN
	has_cond1.settrue
	has_cond2.setfalse
	has_cond3.setfalse
	program check_conds
END

SCRIPT cond2
BEGIN
	has_cond1.setfalse
	has_cond2.settrue
	has_cond3.setfalse
	program check_conds
END

SCRIPT cond3
BEGIN
	has_cond1.setfalse
	has_cond2.setfalse
	has_cond3.settrue
	program check_conds
END


PROGRAM check_conds
BEGIN

if (has_cond1|has_cond2){
call main.ofpr.hide
call main.perc.hide

}

if has_cond3{
call main.ofpr.show
call main.perc.show

}



END

LIST cb_nor
BEGIN
  Normalised
  Not normalised
  
END

LIST cb_nor_val
BEGIN
  nor
  not
END


LIST cb_rel
BEGIN
  The population
  The group
  
END

LIST cb_rel_val
BEGIN
  popul
  group
END

LIST cb_pl
BEGIN
  Mean
  Median
  Quantile
END

LIST cb_pl_val
BEGIN
  mean
  median
  quantile
END


LIST cb_index
BEGIN
  FGT index
  EDE-FGT index
END

LIST cb_index_val
BEGIN
 fgt
 ede
END

DIALOG confop,  tabtitle("Confidence Interval")

BEGIN


TEXT     sp_txt  10      25      120	  .,		                /*
   		*/ label("Confidence level in (%) : ")	
   		
SPINNER  sp_level    140       25      50	  .,		        /*
   		*/ label("Confidence level")				/*
   		*/ min(10) default(95) max(99)				/*
  		*/ option(level)
TEXT     ci_txt  10      +25      120	  .,		                /*
   		*/ label("Confidence interval      : ")	
   		
COMBOBOX cb_ci       140    @   200      .,                 ///
            contents(cb_ci) values(cb_ci_val)              ///
            dropdownlist default(ts)   option(conf)		

					


END


LIST cb_ci
BEGIN
  Two-sided
  Lower-bounded
  Upper-bounded
END


LIST cb_ci_val
BEGIN
  ts
  lb
  ub
END



DIALOG resop,  tabtitle("Results")

BEGIN
					
SPINNER  sp_dec    10       25      50	  .,		        /*
   		*/ label("Decimals")				/*
   		*/ min(0) default(6) max(12)				/*
  		*/ option(dec)

TEXT     sp_txt  70      25      100	  .,		                /*
   		*/ label("Number of decimals")	
   		


END






HELP hlp1,   view("help ifgt")
RESET res1
OK ok1,      label("OK")
CANCEL can1, label("Cancel")
SUBMIT sub1, label("Submit")
COPY copy1

PROGRAM command
BEGIN
	put "ifgt "
	require  main.vl_var
	varlist [main.vl_var]
	beginoptions
		       
			optionarg main.ed_al
			
			optionarg main.vn_hhs
			optionarg main.vn_hhg	
			if !main.cb_index.isdefault() {
						       optionarg main.cb_index
			                              }	
			if !main.cb_nor.isdefault() {
		        optionarg main.cb_nor
			}
			if !resop.sp_dec.isdefault() {
			optionarg resop.sp_dec
			}
			
			if !confop.sp_level.isdefault() {
					                 optionarg confop.sp_level
							 }
				                 
			if !confop.cb_ci.isdefault()    {
					                 optionarg confop.cb_ci
				                        } 
			
			if main.rb_a  {
			               optionarg main.ed_pl
			               }
			
			if !main.rb_a {
			               optionarg main.cb_pl 
			               optionarg main.prop
			               optionarg main.perc
			               if !main.cb_rela.isdefault() {
			               optionarg main.cb_rela
			               }
			              }
			       
	endoptions
	
	
	
END
